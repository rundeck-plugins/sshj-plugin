plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    alias(libs.plugins.axionRelease)
    alias(libs.plugins.nexusPublish)
}

group = 'org.rundeck.plugins'
ext.pluginClassNames='com.plugin.sshjplugin.SSHJNodeExecutorPlugin,com.plugin.sshjplugin.SSHJFileCopierPlugin'
ext.pluginName = 'SSHJ Plugin'
ext.pluginDescription = 'SSH Node Executor and File Copier plugin based on SSHJ library.'
ext.rundeckPluginVersion= '1.2'
ext.publishName = "SSHJ Plugin ${project.version}"
ext.githubSlug = 'rundeck-plugins/sshj-plugin'
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

allprojects {
    project.version = scmVersion.version
    apply from: "${rootDir}/gradle/java.gradle"
}

defaultTasks 'clean','build'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation{
        extendsFrom pluginLibs
    }
}

dependencies {
    pluginLibs libs.sshj
    pluginLibs libs.asnOne
    pluginLibs libs.eddsa
    pluginLibs libs.bundles.bouncycastle
    pluginLibs libs.expectitCore

    implementation libs.commonsIo
    implementation libs.rundeckCore
    implementation libs.slf4jApi

    testImplementation libs.bundles.testLibs
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.x+'
        attributes 'Rundeck-Plugin-Tags': 'java,executor'
        attributes 'Rundeck-Plugin-License': 'MIT'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/sshj-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

test {
    useJUnitPlatform()
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

nexusPublishing {
    packageGroup = 'org.rundeck.plugins'
    repositories {
        sonatype()
    }
}

apply from: "${rootDir}/gradle/publishing.gradle"